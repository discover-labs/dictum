// Dictum Query Language

%import common.SIGNED_FLOAT  -> FLOAT
%import common.SIGNED_INT    -> INTEGER
%import common._STRING_ESC_INNER
STRING: "'" _STRING_ESC_INNER "'"
TRUE: "TRUE"i
FALSE: "FALSE"i
?literal: TRUE | FALSE | INTEGER | FLOAT | STRING

QUOTED_IDENTIFIER: "\"" _STRING_ESC_INNER "\""
IDENTIFIER: /[a-z_]\w*/i
identifier: IDENTIFIER | QUOTED_IDENTIFIER
call: identifier ["(" [ literal ("," literal)* ] ")"]

// keywords
_SELECT: "SELECT"i
_WHERE: "WHERE"i
_GROUPBY: /(GROUP\s+)?BY/i
_AS: "AS"i
_IS: "IS"i
_NOT: "NOT"i
_NULL: "NULL"i
_IN: "IN"i

metric: identifier
dimension: identifier
_tran: dimension "." call

grouping: (dimension | _tran) [ alias ]
alias: _AS IDENTIFIER

?filter: _tran | op

op: eq | ne | gt | ge | lt | le | isnull | isnotnull | isin
eq: dimension ("=" | "==") literal
ne: dimension ("!=" | "<>") literal
gt: dimension ">" literal
ge: dimension ">=" literal
lt: dimension "<" literal
le: dimension "<=" literal
isnull: dimension _IS _NULL
isnotnull: dimension _IS _NOT _NULL
isin: dimension _IN _tuple
_tuple: "(" literal ("," literal)* ")"

select: _SELECT metric ( "," metric )*
where: _WHERE filter ( "," filter )*
groupby: _GROUPBY grouping ( "," grouping )*

query: select [ where ] [ groupby ]

%ignore /\s/
